Class BD_BaronOfHell : BDoomMonster {
	protected bool isknight;
	Default	{
		species "BaronOfHell";
		Health 1000;
		Radius 24;
		Height 64;
		Mass 1000;
		Speed 8;
		PainChance 50;
		Monster;
		+FLOORCLIP
		+BOSSDEATH
		SeeSound "baron/sight";
		PainSound "baron/pain";
		DeathSound "baron/death";
		ActiveSound "baron/active";
		Obituary "$OB_BARON";
		HitObituary "$OB_BARONHIT";
		Tag "$FN_BARON";
		bloodcolor "00 70 00";
		gibhealth 60;
	}
	override void PostBeginPlay() {
		super.PostBeginPlay();
		if (GetClassName() == 'BD_HellKnight')
			isknight = true;
		SetBruiserSprite("BARI","KNII");
	}
	protected void SetBruiserSprite(name sprite1, name sprite2) {
		if (isknight)
			sprite = GetSpriteIndex(sprite2);
		else
			sprite = GetSpriteIndex(sprite1);
	}
	states	{
	Death.Fire:
		TNT1 A 0 A_Jump(256,"Death.PlasmaDamage");
		wait;
	Death.GreenFire:
		TNT1 A 0 A_Jump(256,"Death.PlasmaDamage");
		wait;
	Spawn:
		TNT1 A 0 NoDelay SetBruiserSprite("BARI","KNII");
		#### ABB 10 A_Look;
		goto spawn+1;
	See:
		#### A 0 SetBruiserSprite("BARO","KNIT");
		#### ABCDCBA 3 A_Chase;
		loop;
	Melee:
		#### A 0 SetBruiserSprite("ZBOS","SBOS");
		#### a 4 A_FaceTarget;
		#### bcde 3 A_FaceTarget;
		#### f 8 A_BruisAttack;
		Goto See;
	Missile:
		#### A 0 SetBruiserSprite("ZBOS","SBOS");
		#### A 0 A_FaceTarget;
		#### AABB 2 {
			if (bdoom_flames)
				A_SpawnItemEx("BD_ParticleFire_Green",4,29,frandom(60,62),frandom(0,1),frandom(0,1),frandom(1,2),0);
		}
		#### A 0 A_FaceTarget;
		#### CCDD 2 {
			if (bdoom_flames)
				A_SpawnItemEx("BD_ParticleFire_Green",4,31,frandom(46,48),frandom(0,1),frandom(0,1),frandom(1,2),0);
		}
		#### A 0 A_BruisAttack;
		#### EEFF 2 {
			if (bdoom_flames)
				A_SpawnItemEx("BD_ParticleFire_Green",10,frandom(-4,-6),frandom(24,26),frandom(0,1),frandom(0,1),frandom(1,2),0);
		}
		Goto See;
	Pain:
		#### A 0 SetBruiserSprite("BS1P","BS2P");
		#### a 2;
		#### b 2 A_Pain;
		Goto See;		
	Death:
		TNT1 A 0 {
			bSPRITEFLIP = random[bdsfx](0,1);
			return A_Jump(256,"Death.Vanilla","Death1","Death2");
		}
	Death.Vanilla:
		#### A 0 SetBruiserSprite("BS1M","BS2M");
		#### A 3;
		#### b 3 {
			if (bdoom_blood && !burning)
				A_SpawnItemEx("BD_Blood",frandom(-4,4),frandom(-4,4),random(8,32),frandom(2,4),frandom(2,4),frandom(1,4),random(0,360),SXF_SETTARGET);
		}
		#### c 3 A_scream;
		#### defg 3 {
			if (bdoom_blood && !burning)
				A_SpawnItemEx("BD_Blood",frandom(-4,4),frandom(-4,4),random(8,32),frandom(2,4),frandom(2,4),frandom(1,4),random(0,360),SXF_SETTARGET,80);
		}
		#### h 3 A_NoBlocking;
		#### ijklm 3 {
			if (bdoom_blood && !burning)
				A_SpawnItemEx("BD_Blood",frandom(-4,4),frandom(-4,4),random(8,32),frandom(2,4),frandom(2,4),frandom(1,4),random(0,360),SXF_SETTARGET,100);
		}
		goto deathbleed;
	Death1: //in halves
		#### A 0 SetBruiserSprite("BS1N","BS2N");
		#### A 3;
		#### b 3 {
			if (bdoom_blood && !burning)
				A_SpawnItemEx("BD_Blood",frandom(-4,4),frandom(-4,4),random(14,32),frandom(-2,2),frandom(-2,2),frandom(4.5,7),0,SXF_SETTARGET,80);
		}
		#### c 3 {
			A_scream();
			if (bdoom_blood && !burning)				
				A_SpawnItemEx("BD_Blood",frandom(-4,4),frandom(-4,4),random(14,32),frandom(-2,2),frandom(-2,2),frandom(4.5,7),0,SXF_SETTARGET,80);
		}
		#### defg 3 {
			if (bdoom_blood && !burning)
				A_SpawnItemEx("BD_Blood",frandom(-4,4),frandom(-4,4),random(14,32),frandom(-2,2),frandom(-2,2),frandom(4.5,7),0,SXF_SETTARGET,64);
		}
		#### h 3 A_NoBlocking;
		#### ijkl 3 {
			if (bdoom_blood && !burning)
				A_SpawnItemEx("BD_Blood",frandom(-4,4),frandom(-4,4),random(14,32),frandom(-2,2),frandom(-2,2),frandom(4.5,7),0,SXF_SETTARGET,64);
		}
		goto deathbleed;
	Death2: //fall back
		#### A 0 SetBruiserSprite("BS1O","BS2O");
		#### A 3;
		#### b 3 {
			if (bdoom_blood && !burning)
				A_SpawnItemEx("BD_Blood",frandom(-4,4),frandom(-4,4),random(18,40),frandom(3,5),frandom(-2,2),frandom(2,4),0,SXF_SETTARGET);
		}
		#### c 3 A_scream;
		#### defg 3 {
			if (bdoom_blood && !burning)
				A_SpawnItemEx("BD_Blood",frandom(-4,4),frandom(-4,4),random(18,40),frandom(3,5),frandom(-2,2),frandom(2,4),0,SXF_SETTARGET,64);
		}
		#### h 3 A_NoBlocking;
		#### iJk 3;
		goto deathbleed;
	XDeath:
		#### A 0 SetBruiserSprite("BS1X","BS2X");
		#### A 3;
		#### b 3 {
			if (bdoom_blood && !burning)
				A_SpawnItemEx("BD_Blood",frandom(-4,4),frandom(-4,4),random(14,36),frandom(-4,4),frandom(-4,4),frandom(2,4.5),0,SXF_SETTARGET);
		}
		#### c 3 A_scream;
		#### defg 3 {
			if (bdoom_blood && !burning)
				A_SpawnItemEx("BD_Blood",frandom(-4,4),frandom(-4,4),random(14,36),frandom(-4,4),frandom(-4,4),frandom(2,4.5),0,SXF_SETTARGET,80);
		}
		#### h 3 A_NoBlocking;
		#### ijklMN 2  {
			if (bdoom_blood && !burning)
				A_SpawnItemEx("BD_Blood",frandom(-4,4),frandom(-4,4),random(14,36),frandom(-4,4),frandom(-4,4),frandom(2,4.5),0,SXF_SETTARGET,80);
		}
		#### OP 2;
		#### A 0 A_StartSound("gibs/meatland");
		#### Q 1;
		goto deathbleed;
	Crush:
		CRS3 G -1;
		stop;
	}
}

Class BD_HellKnight : BD_BaronOfHell {
	Default {
		Health 500;
		-BOSSDEATH;
		SeeSound "knight/sight";
		ActiveSound "knight/active";
		PainSound "knight/pain";
		DeathSound "knight/death";
		HitObituary "$OB_KNIGHTHIT";
		Obituary "$OB_KNIGHT";
		Tag "$FN_HELL";
		bloodcolor "00 70 00";
		gibhealth 60;
	}
	States {
	Loadsprites:
		//baron:
		ZBOS A 0;
		BARI A 0;
		BARO A 0;
		BS1M A 0;
		BS1P A 0;
		BS1O A 0;
		BS1N A 0;
		BS1X A 0;
		//knight:
		SBOS A 0;
		KNII A 0;
		KNIT A 0;
		BS2M A 0;
		BS2P A 0;
		BS2O A 0;
		BS2N A 0;
		BS2X A 0;
	}
}


Class BD_BaronBall : BaronBall {
	int broll;
	Default  {
		+BLOODLESSIMPACT
		+ROLLSPRITE
		damagetype "GreenFire";
		decal "BaronScorch";
		renderstyle "Add";
		scale 1.4;
	}
	override void PostBeginPlay () {
		super.PostBeginPlay();
		if (bdoom_projectiles < 2)
			A_SetScale(1);
		if (bdoom_debris) {
			let f = BD_ProjFlare(Spawn("BD_ProjFlare",pos));
			f.fcolor = "green";
			f.master = self;
			if (bdoom_projectiles == 2) {
				f.alpha = 0.6;
				f.scale = (0.12,0.12);
			}
			else {
				f.alpha = 0.35;
				f.scale = (0.1,0.1);
			}
		}
		broll = random(14,20)*randompick(-1,1);
	}
	States	{
	Spawn:
		TNT1 A 0 NoDelay A_JumpIf(bdoom_projectiles < 2,"Spawn.Classic");
		BARB AABB 1 bright {
			if (bdoom_debris)
				A_SpawnItemEx("BD_BaronFireParticle",frandom(8,12),frandom(-4,4),frandom(-4,4),frandom(0.5,0.8),0,0,180,0);
		}
		loop;
	Spawn.Classic:
		BAL7 AB 4 bright;
		loop;
	Death:
		TNT1 A 0 A_JumpIf(bdoom_projectiles < 2,"Death.Classic");
		BARB CDEFGH 3 bright A_SetRoll(roll+broll,SPF_INTERPOLATE);
		stop;
	Death.Classic:
		BPBA ABCDEFGHI 1 bright;
		stop;
	}
}

Class BD_BaronFireParticle : BD_ImpFireParticle {
	Default {
		translation "0:255=%[0.00,0.00,0.00]:[0.32,1.98,0.75]";
		scale 0.3;
	}
}

