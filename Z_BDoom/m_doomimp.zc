Class BD_DoomImp : BDoomMonster {
	Default	{
		species "DoomImp";
		Health 60;
		gibhealth 50;
		Radius 20;
		Height 56;
		Mass 100;
		Speed 8;
		PainChance 200;
		Monster;
		+FLOORCLIP
		SeeSound "imp/sight";
		PainSound "imp/pain";
		DeathSound "imp/death";
		ActiveSound "imp/active";
		HitObituary "$OB_IMPHIT";
		Obituary "$OB_IMP";
		Tag "$FN_IMP";
	}
	/*void BD_TroopAttack() {
		if (bdoom_projectiles < 1)
			A_TroopAttack();
		else
			A_CustomComboAttack("BD_DoomImpBall", 32, 3 * random(1, 8), "imp/melee");
	}*/
	states	{
	Death.Fire:
		stop;
	Spawn:
		TROR AB 10 A_Look();
		Loop;
	See:
		TRO2 ABCDEF 3 A_Chase();
		Loop;
	Melee: //16 face target, 6 attack
		TRO2 I 1 a_facetarget();
		TRO2 IIJKL 3 a_facetarget();
		TRO2 M 3 A_TroopAttack;
		TRO2 LKJ 1;
		Goto See;	
	Missile:
		TNT1 A 0 a_facetarget();
		TRO2 IIJJ 2 {
			if (bdoom_flames)
				A_SpawnItemEx("ParticleFire_DoomImp",3,27,31,	0,0,frandom(0.8,1.0),0);
		}
		TNT1 A 0 a_facetarget();
		TRO2 KKLL 2 {
			if (bdoom_flames)
				A_SpawnItemEx("ParticleFire_DoomImp",12,16,33,	0,0,frandom(0.8,1.0),0);
		}
		TRO2 M 3 A_TroopAttack;
		TRO2 J 1 {
			if (bdoom_flames)
				A_SpawnItemEx("ParticleFire_DoomImp",24,0,36,	0,0,frandom(0.8,1.0),0);
		}
		TRO2 K 1;
		TRO2 L 1 {
			if (bdoom_flames)
				A_SpawnItemEx("ParticleFire_DoomImp",24,0,35,	0,0,frandom(0.8,1.0),0);
		}
		Goto See;
	Pain:
		TRAI a 2;
		TRAI b 2 A_Pain();
		Goto See;
	Death:
		TNT1 A 0 {
			scale.x *= randompick[bdsfx](-1,1);
			if (hitheight > default.height*0.8 && bdoom_gibs && !burning)
				return ResolveState("DeathHeadshot");
			else if (random(1,3) == 3)
				return ResolveState("Death2");
			return A_Jump(256,"Death.Vanilla","Death1");
		}
	Death.Vanilla:
		TROM A 2 {
			A_Scream();
			BD_Bleed();
		}
		TROM B 2;
		TROM C 2 {
			A_NoBlocking();
			BD_Bleed();
		}
		TROM DEFG 2;
		TNT1 A 0 BD_Bleed();
		TROM HIJKL 3;
		goto deathbleed;
	Death1: //fall other side
		TRON A 3 {
			A_Scream();
			BD_Bleed();
		}
		TRON B 2 {
			A_NoBlocking();
			BD_Bleed();
		}
		TRON CDE 3;
		TNT1 A 0 BD_Bleed();
		TRON FGHIJ 3;
		goto deathbleed;
	Death2: //stomach rip
		TROP A 3 A_Scream();
		TROP BC 3 BD_Bleed();
		TROP CCDDCE random(3,7) {
			if (!burning && random(0,100) > 40) {
				A_StartSound("gibs/bloodspurt");
				A_SpawnItemEx("BD_FrontalSpurt",2,frandom(-0.3,0.3),frandom(14,15),0,0,0,random(-30,-40)*scale.x,SXF_SETTARGET,80);
			}
		}
		TNT1 A 0 {
			A_NoBlocking();
			A_Pain();
			}
		TROP FFF random(3,7) {
			if (!burning && random(0,100) > 40) {
				A_StartSound("gibs/bloodspurt");
				A_SpawnItemEx("BD_FrontalSpurt",2,frandom(0,0.5),frandom(11,12),0,0,0,random(-30,-40)*scale.x,SXF_SETTARGET,80);
			}
		}
		TROP GHI 3;
		TROP J 3 A_StartSound("body/fall");
		TROP K 3;
		goto deathbleed;
	DeathHeadshot:
		TROD A 3 {
			A_SpawnItemEx("BD_Gib_Head",frandom(-2,2),frandom(-2,2),frandom(50,54),	frandom(2,5),0,frandom(1,5),random(150,210),SXF_SETTARGET);
			A_SpawnItemEx("BD_Gib_Hand",0,-20*scale.x,25,								vel.x*0.35,frandom(-2,-3)*scale.x,frandom(2,3),	0,SXF_SETTARGET);
		}
		TROD BCDF 3 BD_Bleed();
		TROD F 3 A_NoBlocking();
		TROD GHIJ 3;
		goto deathbleed;
	Death.Saw:
		TNT1 A 0 {
			scale.x *= randompick[bdsfx](-1,1);
		}
		TROY ABC 3;
		TROY D 3 A_NoBlocking();
		TROY EFG 3;
		TROY HIJKL 3 A_SpawnItemEx("BD_Blood",1,0,frandom(18,42),frandom(2,4),0,frandom(4,6),random(-10,-15)*scale.x,SXF_SETTARGET);
		TROY MN 2;
		TROY O 2 {
			A_StartSound("gibs/meatland",slot:CHAN_7);
			A_StartSound("body/fall",slot:CHAN_6);
		}
		TROY PQRS 3;
		goto deathbleed;
	XDeath:
		TNT1 A 0 A_JumpIf(bdoom_gibs==1,"XDeath.Particles");
	XDeath.Vanilla:
		TGIB ab 2;
		TGIB cd 2 a_xscream();
		TGIB ef 2;
		TGIB gh 2 A_NoBlocking();
		TGIB ijklmnO 2;
		goto deathbleed;
	}
}

Class ParticleFire_DoomImp : BD_BaseParticleFire {
	Default {
		renderstyle "Add";
		scale 0.3;
		alpha 0.5;
	}
states {
	Spawn:
		TNT1 A 0 NoDelay A_SetScale(scale.x*randompick(-1,1),scale.y);
		FIPR ABCDEFGHIJKLMNOPQRST 1  A_FadeOut(0.03);
		stop;
	}
}

Class BD_DoomImpBall : DoomImpBall {
	int broll;
	Default {
		+ROLLSPRITE
		+FORCEXYBILLBOARD
		+BLOODLESSIMPACT
		+BRIGHT
		Damagetype "Fire";
		scale 0.5;
		decal "Scorch";
		renderstyle "Add";
		alpha 0.6;
	}
	override void PostBeginPlay () {
		super.PostBeginPlay();
		if (bdoom_projectiles < 2) {
			broll = frandom[bdsfx](6,9) * randompick[bdsfx](-1,1);
			frame = random[bdsfx](0,1);
			alpha = 1;
			A_SetScale(1);
			bSPRITEFLIP = randompick[bdsfx](0,1);
		}
		else if (bdoom_debris) {
			let f = BD_ProjFlare(Spawn("BD_ProjFlare",pos));
			f.fcolor = "yellow";
			f.master = self;
			f.alpha = 0.35;
			f.fscale = 0.2;
		}
	}
	States	{
	Spawn:
		TNT1 A 0 NoDelay A_JumpIf(bdoom_projectiles < 2,"Spawn.Classic");
		IMPB AABBCC 1 {
			if (bdoom_debris)
				A_SpawnItemEx("BD_ImpFireParticle",frandom(-8,-12),frandom(-3,3),frandom(-3,3));
			}
		Loop;
	Spawn.Classic:
		BAL1 # 1 {
			roll += broll;
		}
		loop;
	Death:
		TNT1 A 0 A_JumpIf(bdoom_projectiles < 2,"Death.Classic");
		TNT1 A 0 {
			for (int i = random[bdsfx](5,10); i > 0; i--)
				A_SpawnItemEx(
					"BD_DoomImpBallDebris",
					xvel:frandom[bdsfx](-5,5),
					yvel:frandom[bdsfx](-5,5),
					zvel:frandom[bdsfx](0,5)
				);
		}
		IMPB DEFGH 3  A_FadeOUt(0.1);
		stop;
	Death.Classic:
		TNT1 A 0 { roll = 0; }
		BPIM ABCDEFG 2 bright;
		stop;
	}
}

Class BD_ImpFireParticle : BD_BaseFlare {
	Default {
		scale 0.4;
	}
	states	{
	Spawn:
		FIRP ABCDEFGHIJ 1;
		stop;
	}
}

Class BD_DoomImpBallDebris : BD_ImpFireParticle {
	Default {
		-NOINTERACTION
		gravity 0.65;
	}
	states	{
	Spawn:
		FIRP ABCDEFGHIJ 1 A_FadeOut(0.01);
		wait;
	}
}